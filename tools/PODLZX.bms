# script for QuickBMS http://aluigi.org/papers.htm#quickbms
# extracts POD, PODLZX and PKG archives
# mostly by AlphaTwentyThreee and Chrrox of XeNTaX
# modifications and additions by Acewell

get EXT extension
if EXT == "pod"
	idstring "POD5"
	goto 0x58
	get FILES long
	goto 0x108
	get INFO_TABLE long
	xmath STRING_TABLE_OFFSET "(FILES * 0x1c) + INFO_TABLE"
	goto INFO_TABLE
	for i = 1 <= FILES
	   get NAME_OFFSET long
	   math NAME_OFFSET + STRING_TABLE_OFFSET
	   get SIZE long
	   get OFFSET long
	   get ZSIZE long
	   get COMPRESSED_FLAG long //0 = no, 1 = yes ???
	   get UNK long
	   get UNK long
	   savepos TMP
	   goto NAME_OFFSET
	   get NAME string
	   if SIZE == ZSIZE
		  log NAME OFFSET SIZE
	   else
		  clog NAME OFFSET SIZE ZSIZE
          print "Check if this is correct!, %SIZE%, :size -, %ZSIZE%, :zsize"
	   endif
	   goto TMP
	next i

elif EXT == "podlzx"
	comtype xmemdecompress
	endian big
	get Identifier long
	get Version short
	get Reserved short
    get SIZE asize
    get NAME basename
    string NAME + "_decmp.pod"
    clog NAME 0 SIZE SIZE
	
elif EXT == "pkg"
	for
		set MAGIC 0 
		do
			getdstring MAGIC 4
		while MAGIC != "adoY"
		getdstring MAGIC2 4
		if MAGIC2 == "oMoN"
			cleanexit
		endif
		get SIZE long
		getdstring NAME 0x54
		savepos OFFSET
		log NAME OFFSET SIZE
		math OFFSET + SIZE
		goto OFFSET
		Padding 16
	next
endif